# Maintainer: Sid <sidpranjale127@protonmail.com>
# Continuing the work of:
# Maintainer: Jan Cholasta <jan at cholasta net>

pkgname=pcexhumed-git
pkgver=r14330.970f59d10
pkgrel=1
pkgdesc='Port of the PC version of Exhumed based on EDuke32 (git version)'
arch=('x86_64' 'aarch64')
url='http://nukeykt.retrohost.net/'
license=('GPL' 'custom:BUILD')
depends=('flac'
         'libogg'
         'libvorbis'
         'libvpx'
         'sdl2'
)
makedepends=('git' 'glu' 'imagemagick')
provides=('pcexhumed')
conflicts=('pcexhumed')
options=('!debug' 'strip')
source=('git+https://github.com/NBlood/NBlood.git'
        'pcexhumed.desktop')
sha256sums=('SKIP'
            'e96b49faaca828105416fe636a3196023ba016bde77b9973abaaeeb6ab26b6e2')

pkgver() {
    cd $srcdir/NBlood
    echo "r$(git rev-list --count HEAD).$(git rev-parse --short HEAD)"
}

build() {
    cd NBlood
    make pcexhumed
}

package() {
    cd NBlood
    install -D -t "$pkgdir"/usr/bin pcexhumed
    install -D -t "$pkgdir"/usr/share/games/pcexhumed -m 644 pcexhumed.pk3
    install -D -t "$pkgdir"/usr/share/licenses/$pkgname -m 644 package/common/buildlic.txt
    install -Dm644 platform/Apple/bundles/PCExhumed.app/Contents/Resources/pcexhumed.icns "$pkgdir"/usr/share/pixmaps/pcexhumed.icns
    install -D -t "$pkgdir"/usr/share/applications -m 644 "$srcdir"/pcexhumed.desktop
    echo "Place your game files in ~/.config/pcexhumed"
}
